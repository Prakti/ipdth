defmodule Ipdth.Accounts.User do
  @moduledoc """
  Database Entity representing a User. Has relationships to Agents of the user
  and the Tournaments created by the user.
  """

  use Ecto.Schema
  import Ecto.Changeset

  alias Hex.API.User
  alias Ipdth.Agents.Agent
  alias Ipdth.Tournaments.Tournament
  alias Ipdth.Accounts.User

  @valid_roles [:tournament_admin, :user_admin]
  # TODO: 2024-04-11 - extend schema with :name, :string field (required)
  # TODO: 2024-04-11 - extend schema with timezone and language of user

  schema "users" do
    field :email, :string
    field :password, :string, virtual: true, redact: true
    field :hashed_password, :string, redact: true
    field :confirmed_at, :utc_datetime_usec
    field :roles, {:array, Ecto.Enum}, values: @valid_roles, default: []
    has_many :agents, Agent, foreign_key: :owner_id
    has_many :tournaments, Tournament, foreign_key: :creator_id

    timestamps(type: :utc_datetime_usec)
  end

  @doc """
  A user changeset for registration.

  It is important to validate the length of both email and password.
  Otherwise databases may truncate the email without warnings, which
  could lead to unpredictable or insecure behaviour. Long passwords may
  also be very expensive to hash for certain algorithms.

  ## Options

    * `:hash_password` - Hashes the password so it can be stored securely
      in the database and ensures the password field is cleared to prevent
      leaks in the logs. If password hashing is not needed and clearing the
      password field is not desired (like when using this changeset for
      validations on a LiveView form), this option can be set to `false`.
      Defaults to `true`.

    * `:validate_email` - Validates the uniqueness of the email, in case
      you don't want to validate the uniqueness of the email (like when
      using this changeset for validations on a LiveView form before
      submitting the form), this option can be set to `false`.
      Defaults to `true`.
  """
  def registration_changeset(user, attrs, opts \\ []) do
    user
    |> cast(attrs, [:email, :password])
    |> validate_email(opts)
    |> validate_password(opts)
  end

  defp validate_email(changeset, opts) do
    changeset
    |> validate_required([:email])
    |> validate_format(:email, ~r/^[^\s]+@[^\s]+$/, message: "must have the @ sign and no spaces")
    |> validate_length(:email, max: 160)
    |> maybe_validate_unique_email(opts)
  end

  defp validate_password(changeset, opts) do
    changeset
    |> validate_required([:password])
    |> validate_length(:password, min: 12, max: 72)
    # Examples of additional password validation:
    # |> validate_format(:password, ~r/[a-z]/, message: "at least one lower case character")
    # |> validate_format(:password, ~r/[A-Z]/, message: "at least one upper case character")
    # |> validate_format(:password, ~r/[!?@#$%^&*_0-9]/, message: "at least one digit or punctuation character")
    |> maybe_hash_password(opts)
  end

  defp maybe_hash_password(changeset, opts) do
    hash_password? = Keyword.get(opts, :hash_password, true)
    password = get_change(changeset, :password)

    if hash_password? && password && changeset.valid? do
      changeset
      # If using Bcrypt, then further validate it is at most 72 bytes long
      |> validate_length(:password, max: 72, count: :bytes)
      # Hashing could be done with `Ecto.Changeset.prepare_changes/2`, but that
      # would keep the database transaction open longer and hurt performance.
      |> put_change(:hashed_password, Bcrypt.hash_pwd_salt(password))
      |> delete_change(:password)
    else
      changeset
    end
  end

  defp maybe_validate_unique_email(changeset, opts) do
    if Keyword.get(opts, :validate_email, true) do
      changeset
      |> unsafe_validate_unique(:email, Ipdth.Repo)
      |> unique_constraint(:email)
    else
      changeset
    end
  end

  @doc """
  Creates the changeset for seeding a user.
  Only for testing and seeding purposes.
  """
  def seed_user_changeset(user, attrs, opts \\ []) do
    user
    |> cast(attrs, [:email, :password, :roles])
    |> validate_email(opts)
    |> validate_password(opts)
    |> validate_subset(:roles, @valid_roles)
    |> confirm_changeset()
  end

  @doc """
  A user changeset for changing the email.

  It requires the email to change otherwise an error is added.
  """
  def email_changeset(user, attrs, opts \\ []) do
    user
    |> cast(attrs, [:email])
    |> validate_email(opts)
    |> case do
      %{changes: %{email: _}} = changeset -> changeset
      %{} = changeset -> add_error(changeset, :email, "did not change")
    end
  end

  @doc """
  A user changeset for changing the password.

  ## Options

    * `:hash_password` - Hashes the password so it can be stored securely
      in the database and ensures the password field is cleared to prevent
      leaks in the logs. If password hashing is not needed and clearing the
      password field is not desired (like when using this changeset for
      validations on a LiveView form), this option can be set to `false`.
      Defaults to `true`.
  """
  def password_changeset(user, attrs, opts \\ []) do
    user
    |> cast(attrs, [:password])
    |> validate_confirmation(:password, message: "does not match password")
    |> validate_password(opts)
  end

  @doc """
  Confirms the account by setting `confirmed_at`.
  """
  def confirm_changeset(user) do
    now = DateTime.utc_now() |> DateTime.truncate(:microsecond)
    change(user, confirmed_at: now)
  end

  @doc """
  Verifies the password.

  If there is no user or the user doesn't have a password, we call
  `Bcrypt.no_user_verify/0` to avoid timing attacks.
  """
  def valid_password?(%User{hashed_password: hashed_password}, password)
      when is_binary(hashed_password) and byte_size(password) > 0 do
    Bcrypt.verify_pass(password, hashed_password)
  end

  def valid_password?(_, _) do
    Bcrypt.no_user_verify()
    false
  end

  @doc """
  Validates the current password otherwise adds an error to the changeset.
  """
  def validate_current_password(changeset, password) do
    if valid_password?(changeset.data, password) do
      changeset
    else
      add_error(changeset, :current_password, "is not valid")
    end
  end

  @doc """
  Adds a role to the user, but only if it's valid.
  """
  def add_role(user, role) do
    role = ensure_role_is_atom(role)
    changeset = change(user)

    current_roles = get_field(changeset, :roles) || []
    new_roles = Enum.uniq([role | current_roles])

    changeset
    |> put_change(:roles, new_roles)
    |> validate_subset(:roles, Ecto.Enum.values(User, :roles))
  end

  @doc """
  Removes a valid role from the user.
  """
  def remove_role(user, role) do
    role = ensure_role_is_atom(role)
    changeset = change(user)

    if Enum.member?(@valid_roles, role) do
      current_roles = get_field(changeset, :roles) || []
      new_roles = Enum.filter(current_roles, fn existing_role -> existing_role != role end)

      changeset
      |> put_change(:roles, new_roles)
      |> validate_subset(:roles, Ecto.Enum.values(User, :roles))
    else
      changeset
      |> add_error(:roles, "role '%{role}' does not exist", role: role)
    end
  end

  def get_available_roles(), do: @valid_roles

  @doc """
  Sometimes we get the string repesentation of a role.
  We can use this to find the corresponding role atom from all the valid
  roles.

  Note: simply converting from string to atom is discouraged, as this could
  blow up memory.
  """
  def ensure_role_is_atom(maybe_role) do
    Enum.find(@valid_roles, maybe_role, fn valid_role ->
      to_string(valid_role) == maybe_role
    end)
  end
end
